using System.Collections.Generic;
using System.Globalization;
using SKON.Internal.Utils;

COMPILER SKEMA

	public SKONObject metadata = new SKONObject();

    public SKEMAObject data = SKEMAObject.Any;

	public Dictionary<string, SKEMAObject> definitions = new Dictionary<string, SKEMAObject>();

	private string[] dateTimeFormats = {
        "yyyy-MM-dd",
        "hh:mm:ssZ",
        "hh:mm:ss.fffZ",
        "hh:mm:sszzz",
        "hh:mm:ss.fffzzz",
        "yyyy-MM-ddThh:mm:ssZ",
        "yyyy-MM-ddThh:mm:ss.fffZ",
        "yyyy-MM-ddThh:mm:sszzz",
        "yyyy-MM-ddThh:mm:ss.fffzzz"
    };

    private DateTime ParseDatetime(string value)
    {
		value = value.Substring(1);

        DateTime dateTime;

        if (DateTime.TryParseExact(value, dateTimeFormats, null, DateTimeStyles.None, out dateTime))
        {
            return dateTime;
        }
        else
        {
            return ParserUtils.UnixTimeStampToDateTime(long.Parse(value));
        }
    }

/*-------------------------------------------------------------------------*/
CHARACTERS
  letter    = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_".
  digit     = "0123456789".
  cr        = '\r'.
  lf        = '\n'.
  tab       = '\t'.  
  stringch  = ANY - '"' - '\\' - cr - lf.
  charch    = ANY - '\'' - '\\' - cr - lf.
  printable =  '\u0020' .. '\u007e'.
  escapech    = "bnfrt/" + '\\' + '"'.  
  hex       = "0123456789abcdefABCDEF".
  dot       = '.'.

TOKENS
  tilda     = '~'.
  colon     = ':'.
  comma     = ','.
  lbrace    = '{'.
  rbrace    = '}'.
  lbracket  = '['.
  rbracket  = ']'.
  ident     = letter { letter | digit } CONTEXT (":").
  string_   = '"' { stringch | '\\' escapech | '\\' 'u' hex hex hex hex} '"'.
  badString = '"' { stringch | '\\' escapech } (cr | lf).
  integer_  = ['-'] digit {digit}.
  double_   = ['-'] digit {digit} (((dot digit {digit}) [('E' | 'e') [('+' | '-')] digit {digit}]) | (('E' | 'e') [('+' | '-')] digit {digit})).
  datetime_	= '@' 
			(digit {digit}
			| digit digit digit digit '-' digit digit '-' digit digit
			| digit digit ':' digit digit ':' digit digit ('Z'|'z')
			| digit digit ':' digit digit ':' digit digit '.' digit digit digit ('Z'|'z')
			| digit digit ':' digit digit ':' digit digit ('+'|'-') digit digit ':' digit digit
			| digit digit ':' digit digit ':' digit digit '.' {digit} ('+'|'-') digit digit ':' digit digit
			| digit digit digit digit '-' digit digit '-' digit digit ('T' | 't') digit digit ':' digit digit ':' digit digit ('Z'|'z')
			| digit digit digit digit '-' digit digit '-' digit digit ('T' | 't') digit digit ':' digit digit ':' digit digit '.' digit digit digit ('Z'|'z')
			| digit digit digit digit '-' digit digit '-' digit digit ('T' | 't') digit digit ':' digit digit ':' digit digit ('+'|'-') digit digit ':' digit digit
			| digit digit digit digit '-' digit digit '-' digit digit ('T' | 't') digit digit ':' digit digit ':' digit digit '.' digit digit digit ('+'|'-') digit digit ':' digit digit
			).

  ref = '#' letter { letter | digit }.
  def = "define".
  opt = "optional".

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf

IGNORE ' ' + '\r' + '\t' + '\n'

PRODUCTIONS
/*-------------------------------------------------------------------------*/
SKEMA																(. Dictionary<string, SKONObject> metadataElements = new Dictionary<string, SKONObject>();
																		Dictionary<string, SKEMAObject> mapElements;
																		Dictionary<string, bool> optionalMap;
																		string key; SKONObject value; .)
=	{ meta_data<out key, out value> }								(. this.metadata = new SKONObject(metadataElements); .)
	open_skema_map<out mapElements, out optionalMap>				(. this.data = new SKEMAObject(mapElements, optionalMap); .)
    .															

/*-------------------------------------------------------------------------*/
meta_data<out string key, out SKONObject obj>
=	tilda
	skon_map_element<out key, out obj>
	tilda.

/*-------------------------------------------------------------------------*/
skema_map<out SKEMAObject map>										(. Dictionary<string, SKEMAObject> mapElements; Dictionary<string, bool> optionalMap; .)
=	lbrace
	open_skema_map<out mapElements, out optionalMap>				(. map = new SKEMAObject(mapElements, optionalMap); .)
	rbrace
	.

/*-------------------------------------------------------------------------*/
skon_map<out SKONObject map>										(. Dictionary<string, SKONObject> mapElements;  .)
=	lbrace
    open_skon_map<out mapElements>									(. map = new SKONObject(mapElements); .)
    rbrace
    .

/*-------------------------------------------------------------------------*/
skema_array<out SKEMAObject array>									(. SKEMAObject skemaObj; .)
=	lbracket
	skema_value<out skemaObj>										(. array = SKEMAObject.ArrayOf(skemaObj); .)
	rbracket
	.

/*-------------------------------------------------------------------------*/
skon_array<out SKONObject array>									(. List<SKONObject> arrayElements; .)
=	lbracket
    open_skon_array<out arrayElements>								(. array = new SKONObject(arrayElements); .)
    rbracket
    .

/*-------------------------------------------------------------------------*/
open_skema_map<. out Dictionary<string, SKEMAObject> mapElements, out Dictionary<string, bool> optionalMap .>	(. string key; SKEMAObject value; bool optional; mapElements = new Dictionary<string, SKEMAObject>(); optionalMap = new Dictionary<string, bool>(); .)
=	{
		(
			skema_map_element<out key, out value, out optional>													(. mapElements[key] = value; if(optional) { optionalMap[key] = true; } .)
			| definition<out key, out value>																	(. definitions[key] = value; .)
		)							
		WEAK comma
	}
	.

/*-------------------------------------------------------------------------*/
open_skon_map<. out Dictionary<string, SKONObject> mapElements .>	(. string key; SKONObject value; mapElements = new Dictionary<string, SKONObject>(); .)
=	{
        skon_map_element <out key, out value>						(. mapElements[key] = value; .)
		WEAK comma
    }
    .

/*-------------------------------------------------------------------------*/
skema_map_element<out string key, out SKEMAObject obj, out bool optional>	(. optional = false; .)
=	[
		opt																	(. optional = true; .)
	]
	Ident<out key>													
    colon 
    skema_value<out obj>											
    .

/*-------------------------------------------------------------------------*/
skon_map_element<out string key, out SKONObject obj>				
=	Ident<out key>													
    colon 
    skon_value<out obj>												
    .

/*-------------------------------------------------------------------------*/
definition<out string key, out SKEMAObject def>
=	def Ident<out key> 
	colon
	skema_value<out def>
	.

/*------------------------------------------------------------------------*/
Ident<out string name>
= ident																(. name = t.val; .)
  .

/*-------------------------------------------------------------------------*/
open_skon_array<. out List<SKONObject> arrayElements .>				(. SKONObject skonObject; arrayElements = new List<SKONObject>(); .)
=	{
        skon_value<out skonObject>									(. arrayElements.Add(skonObject); .)
		WEAK comma 
    }
    .

/*-------------------------------------------------------------------------*/
skon_value<out SKONObject skonObject>								(. skonObject = null; .)
=	string_															(. skonObject = new SKONObject(ParserUtils.EscapeString(t.val.Substring(1, t.val.Length - 2))); .)
    | integer_														(. skonObject = new SKONObject(int.Parse(t.val)); .)
    | double_														(. skonObject = new SKONObject(double.Parse(t.val, CultureInfo.InvariantCulture)); .)
    | datetime_														(. skonObject = new SKONObject(ParseDatetime(t.val)); .)
	| skon_map<out skonObject>									
    | skon_array<out skonObject>								
    | "true"														(. skonObject = new SKONObject(true); .)
    | "false"														(. skonObject = new SKONObject(false); .)
    | "null"														(. skonObject = new SKONObject(); .)
    .	
												
/*-------------------------------------------------------------------------*/
type<out SKEMAObject skemaObj>										(. skemaObj = null; .)
=	"Any"															(. skemaObj = SKEMAObject.Any; .)
	| "String"														(. skemaObj = SKEMAObject.String; .)
	| "Integer"														(. skemaObj = SKEMAObject.Integer; .)
	| "Float"														(. skemaObj = SKEMAObject.Float; .)
	| "Boolean"														(. skemaObj = SKEMAObject.Boolean; .)
	| "DateTime"													(. skemaObj = SKEMAObject.DateTime; .)
	.

/*-------------------------------------------------------------------------*/
skema_value<out SKEMAObject skemaObj>								(. skemaObj = null; .)
=	type<out skemaObj>
	| skema_map<out skemaObj>
	| skema_array<out skemaObj>
	| ref															(. skemaObj = new SKEMAObject(t.val.Substring(1)); .)
	.

END SKEMA.